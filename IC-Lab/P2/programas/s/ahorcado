.data
.equ MAX_FALLOS, 6
PALABRA: .space 7
.align 2
OCULTA: .space 7
.align 2
introduccion: .asciz "Bienvenido al juego del ahorcado.\n"
turno: .asciz "Escoge una letra:\n"
victoria: .asciz "¡Felicidades!Has adivinado la palabra.\n"
derrota: .asciz "No has conseguido adivinar la palabra. ¡Juega de nuevo!\n"

.include "ConstPalabras6Car.s"
.text
.extern random
.extern listadoPalabras6Car
.extern funciones1
.globl _start

_start:
        ldr r0, =introduccion @Bienvenida al videojuego
        swi 0x02

PARTIDA:
        @rutina que imprime menu del juego
        bl menu

OPCION:
        @escogemos jugar o salir del programa
        swi 0x04

        cmp r0, #0x31
        blt OPCION   @gestion de errores

        cmp r0, #0x32
        beq SALIR
        bhi OPCION   @gestion de errores

        @debemos seleccionar una palabra aleatoria entre todas que las que hay en la lista
        mov r0, #NUM_PALABRAS
        ldr r1, =PALABRAS
        mov r2, #OFF_PALABRA
        bl selecciona_palabra

        @almacenamos la palabra y la palabra oculta en memoria inicializadas.
        ldr r1, =PALABRA
        ldr r2, =OCULTA
        bl inicializa_palabras

        @pedimos al jugador que juegue su turno y mostramos palabra oculta
        ldr r0, =OCULTA
        bl muestra_turno

        mov r9, #0 @contador fallos

BUCLE:
        @si se exceden los fallos termina
        cmp r9, #MAX_FALLOS
        beq BUCLE_FIN

        @recogemos la eleccion del jugador, almacena en r0
        swi 0x04

        cmp r0, #0x1D @caracter especial
        beq VALIDO
        cmp r0, #0x41
        blt BUCLE @ vuelve a intentar
        cmp r0, #0x5A
        bhi BUCLE @ vuelve a intentar

VALIDO:
       @pasamos los parametros a la subrutina para saber si hubo acierto o fallo
       ldr r1, =PALABRA
       ldr r2, =OCULTA
       bl comprueba

       @comparamos el resultado del turno. Si vale 1 entonces hubo acierto,  0 en caso contrario.
       cmp r0, #1
       addne r9, r9, #1  @numero de fallos

       @comprobamos el estado de la palabra. Si la hemos adivinado, salimos del bucle.
       ldr r0, =OCULTA
       bl estado
       cmp r0, #1
       beq BUCLE_FIN @salimos del bucle al haber adivinado la palabra

       @pedimos al usuario que juege otro turno
       ldr r0, =OCULTA
       bl muestra_turno
       b BUCLE

BUCLE_FIN:

       @imprimimos mensaje de perdida o victoria y despues, la palabra en si
       cmp r9, #MAX_FALLOS

       ldreq r0, =derrota
       ldrne r0, =victoria
       swi 0x02
       mov r0, #'\n'
       swi 0x00

       ldr r0, =PALABRA
       swi 0x02

       mov r0, #'\n'
       swi 0x00
       b PARTIDA

SALIR:
      swi 0x11
